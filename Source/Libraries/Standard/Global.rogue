class Global : BufferedPrintWriter<<global_output_buffer>> [singleton]
  PROPERTIES
    standard_output : PrintWriter
    exit_functions  : Function[]

  METHODS
    method init
      native @|signal( SIGINT, RogueGlobal__on_control_c__RogueInt32 );
      configure_standard_output
      on_exit( () => Global.flush )

    method configure_standard_output
      standard_output = Console

    method flush( buffer:String )
      standard_output.[ print(buffer), flush ]
      buffer.clear

    method on_exit( fn:Function )
      if (not exit_functions) exit_functions = Function[]
      exit_functions.add( fn )

  GLOBAL METHODS
    method call_exit_functions [essential]
      # Called by the native layer when the program is ending normally or exiting
      # due to a System.exit(Int32) call.
      # Ensure these functions won't be called multiple times
      local functions = Global.exit_functions
      Global.exit_functions = null

      if (functions)
        forEach (fn in functions)
          fn()
        endForEach
      endIf

    method on_control_c( signum:Int32 ) [essential]
      call_exit_functions
      native @|exit(1);
endClass
