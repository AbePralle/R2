module Rogue

class Collector : Visitor
  PROPERTIES
    autoname_index = 0

  METHODS
    method autoname( base:String )->String
      local result = "_auto_$_$"(base,autoname_index)
      ++autoname_index
      return result

    method on( cmd:Cmd )->Cmd
      throw cmd.t.error( "[INTERNAL] Unexpected $ in Collector."(cmd.type_name) )

    method on( cmd:DefineModule )->Cmd
      if (cmd.name) Program.current_module = Program.get_module( cmd.t, cmd.name )
      else          Program.current_module = Program.default_module

      # TODO: copy cmd.attributes into module

      return null

    method on( cmd:Routine )->Cmd
      cmd.organize
      return null

    method on( cmd:Statements )->Cmd
      Program.current_module.global_statements.add( forEach in cmd )
      return null

    method on( cmd:UsesModule )->Cmd
      local m = Program.get_module( cmd.t, File.filename(cmd.path) )
      Program.current_module.uses_module( m, &is_exported=cmd.attributes.is_exported )
      return null
endClass

