module Rogue

class Finalizer : Visitor
  PROPERTIES
    next_type_id   = 1
    next_method_id = 1

  METHODS
    method apply
      Program.apply( this )

      forEach (entry in Program.dynamic_methods.entries)
        if (entry.value.count > 1)
          (forEach in entry.value).attributes.dynamic_dispatch = true
        endIf
      endForEach

    method on_visit_type( type:Type )
      if (type.is_aspect) return

      type.id = next_type_id
      ++next_type_id
      prior.on_visit_type( type )

    method on_visit( m:Method )
      if (m.type_context.is_aspect) return

      if (m.type_context is this_type and this_type.is_reference)
        m.id = next_method_id
        ++next_method_id

        if (this_type.base_class)
          local base_m = this_type.base_class.methods[ m.signature ]
          if (base_m) base_m.attributes.is_overridden = true
        endIf

        local full_sig = m.signature
        if (m.return_type) full_sig += "->" + m.return_type
        local methods = Program.dynamic_methods[full_sig]
        if (not methods)
          methods = Method[]
          Program.dynamic_methods[full_sig] = methods
        endIf
        methods.add( m )

      endIf
      visit_children( m )

endClass
