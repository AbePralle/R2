module Rogue

class MethodOrganizer : Visitor
  METHODS
    method init( this_module )

    method visit( this_type, class_def:Cmd )
      # Called when instantiating a template
      visit( class_def )

    method on_visit( cmd:Class )
      if (cmd.constructor_properties)
        if (this_type.is_object)
          # Generate an init() method off the constructor properties.
          local m_init = Method( cmd.t, "init" )
          m_init.parameters.add( forEach in cmd.constructor_properties )
          on( m_init )

        elseIf (this_type.is_compound)
          # Generate a create() global method as the "primary constructor".
          local m_create = GlobalMethod( cmd.t, "create", &return_type=this_type )
          m_create.parameters.add( forEach in cmd.constructor_properties )
          local v = Local( cmd.t, "result", DummyInitialValue, this_type )
          m_create.locals.add( v )
          m_create.body.locals.add( v )
          m_create.statements.add( SetLocal(cmd.t,v,CreateCompound(cmd.t,this_type),&initial_assignment) )
          m_create.statements.add( Return(cmd.t,GetLocal(cmd.t,v)) )
          on( m_create )
        endIf
      endIf

      visit_children( cmd )

    method on_visit( cmd:GlobalMethod )
      cmd.module_context = this_module
      cmd.type_context = this_type
      cmd.organize( this )

    method on_visit_children( cmd:Local )
      prior.on_visit_children( cmd )
      cmd.initial_value = visit( cmd.initial_value )

    method on_visit( cmd:Method )
      cmd.module_context = this_module
      cmd.type_context = this_type
      cmd.organize( this )
      if (this_type.is_compound and cmd.name == "init")
        throw cmd.t.error(
            "Compounds and primitives cannot have init() methods. Add global method create$->$ instead." ...
            (cmd.signature.rightmost(-4),this_type)
        )
      endIf

    method on_visit( cmd:Routine )
      cmd.organize( this )

    method on_visit_children( cmd:Statements )
      forEach (element in writer=cmd.list.rewriter)
        local visited_element = visit( element )
        if (visited_element)
          if (visited_element instanceOf Statements)
            writer.write( forEach in visited_element )
          else
            writer.write( visited_element )
          endIf
        endIf
      endForEach

    method on_visit( cmd:This )
      assert this_procedure
      if (not this_procedure.is_method)
        throw cmd.t.error( "'this' can only be referenced in an object method." )
      endIf
      cmd.this_type = this_type

endClass
