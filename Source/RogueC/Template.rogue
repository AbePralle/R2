module Rogue

class Template
  PROPERTIES
    t            : Token
    name         : String
    placeholders : Token[]
    tokens       = Token[]
    attributes   : Attributes

  METHODS
    method init( t, name, placeholders, tokens, attributes:Int32 )
      this.attributes = Attributes( t )
      this.attributes.flags = attributes

    method description->String
      return name

    method generate( type:Type )
      type.attributes.is_defined = true

      type._template = this
      local template_tokens = tokens

      if (type.type_args)
        assert placeholders.count == type.type_args.count
        template_tokens = Token[]( tokens.count * 1.1 )

        local replacements = Table<<String,Token[]>>()
        forEach (placeholder at i in placeholders)
          replacements[placeholder] = type.type_args[i].tokens
        endForEach

        forEach (t in tokens)
          if (t.type == TokenType.PLACEHOLDER)
            local replacement = replacements[t.content]
            if (replacement)
              template_tokens.add( forEach in replacement )
            else
              throw t.error( "Invalid template placeholder '$'."(t.content) )
            endIf
          else
            template_tokens.add( t )
          endIf
        endForEach
      endIf

      Program.unorganized_types.add( type )
      local unresolved_type_count = Program.unorganized_types.count

      type.class_def = Parser( template_tokens ).parse( Parser.ip_class )

      if (unresolved_type_count == 1)
        local i = 0
        while (i < Program.unorganized_types.count)
          Program.unorganized_types[i].organize_type
          ++i
        endWhile

        i = 0
        while (i < Program.unorganized_types.count)
          Program.unorganized_types[i].organize_methods
          ++i
        endWhile

        Program.unorganized_types.clear
      endIf

      Program.is_modified = true
endClass
