module Rogue

class Type : Cmd
  PROPERTIES
    module_context    : Module
    name              : String
    attributes        : Attributes
    definitions       = StringTable<<Cmd>>()
    global_properties = StringTable<<Property>>()
    properties        = StringTable<<Property>>()
    global_methods    = StringTable<<GlobalMethod>>()
    methods           = StringTable<<Method>>()
    native_properties = String[]
    native_header     = NativeHeader[]
    native_code       = NativeCode[]

  GLOBAL METHODS
    method create( t:Token, name:String )->Type
      local type = Program.current_module.find_type( name, &extended_search )
      if (type) return type
      type = Type( t, name, 0 )
      type.ensure_definition
      return type

  METHODS
    method init( t, name, attributes:Int32, &unlisted )
      init( t, Program.current_module, name, attributes, &=unlisted )

    method init( t, module_context, name, attributes:Int32, &unlisted )
      if (not unlisted) module_context.types[ name ] = this
      this.attributes = Attributes( t, attributes )

    method init( existing:Type )
      prior.init( existing )
      name   = existing.name
      c_name = existing.@c_name
      attributes = existing.attributes
      module_context = existing.module_context

    method add( m:GlobalMethod )
      global_methods[ m.signature ] = m
      # TODO: check for existing methods

    method add( m:Method )
      methods[ m.signature ] = m
      # TODO: check for existing methods

    method cloned->this
      return this

    method collect_methods( m_name:String, list:Procedure[], &require_global )
      forEach (m in global_methods)
        if (m.name == m_name) list.add( m )
      endForEach

      if (not require_global)
        forEach (m in methods)
          if (m.name == m_name) list.add( m )
        endForEach
      endIf

    method ensure_definition
      if (attributes.is_defined) return

      if (is_primitive)
        attributes.is_defined = true # KLUDGE
        return
      endIf

      local template = module_context.templates[ "$<<0>>"(name) ]
      if (template)
        template.generate( null, this )
      else
        throw t.error( "Reference to undefined class '$::$'."(module_context,name) )
      endIf

    method description->String
      return "$" (%name)

    method instance_of( base_class:Type )->Logical
      if (this is base_class) return true
      # TODO: add support for class hierarchy
      return false

    method is_primitive->Logical
      return attributes.is_primitive

    method is_reference->Logical
      return attributes.is_reference

    method organize
      if (attributes.is_organized) return
      attributes.is_organized = true
      ensure_definition

      # TODO: organize base types, validate hierarchy
      # TODO: inherit base class methods

    method resolve
      if (attributes.is_resolved) return
      attributes.is_resolved = true
      organize

    method to->Character
      return Cmd.to_character(name)


    method to->Int32:  return name
    method to->Logical: return name?
    method to->String: return name

endClass

class BuiltInType : Type;

class TypeLogical : BuiltInType
  METHODS
    method init( t:Token )
      prior.init( t, "Logical", Attribute.PRIMITIVE )
endClass

class TypeByte : BuiltInType
  METHODS
    method init( t:Token )
      prior.init( t, "Byte", Attribute.PRIMITIVE )
endClass

class TypeCharacter : BuiltInType
  METHODS
    method init( t:Token )
      prior.init( t, "Character", Attribute.PRIMITIVE )
endClass

class TypeInt32 : BuiltInType
  METHODS
    method init( t:Token )
      prior.init( t, "Int32", Attribute.PRIMITIVE )
endClass

class TypeInt64 : BuiltInType
  METHODS
    method init( t:Token )
      prior.init( t, "Int64", Attribute.PRIMITIVE )
endClass

class TypeReal32 : BuiltInType
  METHODS
    method init( t:Token )
      prior.init( t, "Real32", Attribute.PRIMITIVE )
endClass

class TypeReal64 : BuiltInType
  METHODS
    method init( t:Token )
      prior.init( t, "Real64", Attribute.PRIMITIVE )
endClass

class TypeString : BuiltInType
  METHODS
    method init( t:Token )
      prior.init( t, "String", Attribute.OBJECT )
endClass

