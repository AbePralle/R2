module Rogue

class CSpecializer : Visitor
  PROPERTIES
    writer         : CWriter
    autoname_index = 0

    statement_injector : ListRewriter<<Cmd>>

  METHODS
    method autoname( base:String )->String
      local result = "_auto_$_$"(base,autoname_index)
      ++autoname_index
      return result

    method on_visit( cmd:ControlStructure )
      if (cmd.control_escapes)
        cmd.c_escape_label_name = autoname( "escape" )
      endIf
      visit_children( cmd )

    method on_visit( cmd:LiteralString )
      local str = cmd.value
      if (not Program.unique_strings.contains(str))
        use builder = StringBuilder.pool
          builder.print( "str_" )
          forEach (i in 0..<str.count.or_smaller(20))
            local ch = str[i]
            if (ch.is_letter or ch.is_number) builder.print( ch )
            else builder.print( '_' )
          endForEach
          local c_name = builder->String
          if (Program.c_name_strings.contains(c_name))
            local n = 1
            while (Program.c_name_strings.contains(c_name))
              local c_name_2 = c_name + "_" + n
              if (not Program.c_name_strings.contains(c_name_2))
                c_name = c_name_2
                escapeWhile
              endIf
              ++n
            endWhile
          endIf
          Program.unique_strings[ str ] = c_name;
          Program.c_name_strings[ c_name ] = str;
        endUse
      endIf

    method on_visit( cmd:Procedure )
      autoname_index = 0

      cmd.c_result_name = autoname("result")

      forEach (p at i in cmd.parameters)
        block p = p->(as Local)
          p.c_name = "$_$" (p.name,i)
        endBlock
      endForEach

      local di = cmd.parameters.count
      forEach (v at i in cmd.locals) v.c_name = "$_$"(v.name,i+di)

      visit_children( cmd )

    method on_visit( cmd:Statements )
      temporarily statement_injector = cmd.list.rewriter
        forEach (statement in statement_injector)
          statement = visit( statement )
          if (statement) statement_injector.write( statement )
        endForEach
      endTemporarily

endClass

augment ControlStructure
  PROPERTIES
    c_escape_label_name : String

  METHODS
    method has_cleanup_code->Logical
      forEach (v in locals)
        if (v.type.is_reference)
          return true
        endIf
      endForEach
      return false
endAugment

augment Local
  PROPERTIES
    c_name : String
endAugment

augment Procedure
  PROPERTIES
    c_name        : String
    c_result_name : String

  METHODS
    method c_name->String
      throw UnsupportedOperationError()

    method has_cleanup_code->Logical
      forEach (p in parameters)
        if (p.type.is_reference and not p.is_anchored)
          return true
        endIf
      endForEach

      return body.has_cleanup_code
endAugment

augment Program
  PROPERTIES
    c_name_strings = StringTable<<String>>()
endAugment

augment Type
  PROPERTIES
    c_name : String   # e.g. RogueString
    c_type : String   # e.g. RogueString*

  METHODS
    method c_type->String
      if (@c_type) return @c_type
      @c_type = c_name
      if (is_reference) @c_type += "*"
      return @c_type

    method c_name->String
      if (@c_name) return @c_name
      @c_name = "$$" (module_context.c_name,name)
      return @c_name

endAugment

augment
  METHODS
    method GlobalMethod.c_name->String
      if (@c_name) return @c_name
      use builder = StringBuilder.pool
        builder.print( type_context.c_name ).print( "__" )
        builder.print( name )
        if (parameters.count)
          builder.print '_'
          forEach (p in parameters)
            builder.print( "_" ).print( p.type.c_name )
          endForEach
        endIf
        c_name = builder
        return @c_name
      endUse

    method Method.c_name->String
      if (@c_name) return @c_name
      use builder = StringBuilder.pool
        builder.print( type_context.c_name ).print( "__" )
        builder.print( name )
        if (parameters.count)
          builder.print '_'
          forEach (p in parameters)
            builder.print( "_" ).print( p.type.c_name )
          endForEach
        endIf
        c_name = builder
        return @c_name
      endUse

    method Module.c_name->String
      return name

    method Property.c_name->String
      return name

    method Routine.c_name->String
      if (@c_name) return @c_name
      use builder = StringBuilder.pool
        builder.print( module_context.c_name ).print( "___" )
        builder.print( name )
        if (parameters.count)
          builder.print "_"
          forEach (p in parameters)
            builder.print( "_" ).print( p.type.c_name )
          endForEach
        endIf
        c_name = builder
        return @c_name
      endUse

endAugment

