module Rogue

class Procedure : Cmd
  PROPERTIES
    module_context : Module
    name           : String
    signature      : String
    parameters     : Cmd
    return_type    : Type
    attributes     : Attributes
    body           : Cmd   # body is LocalScope(statements)
    statements     : Cmd   # the Statements contained in the body's LocalScope
    is_resolved    = false

    locals         = Local[]

  METHODS
    method init( t:Token, name:String, parameters=null:Cmd, return_type=null:Type,
        attributes=null:Attributes, body=null:Cmd )
      init( t, Program.current_module, name, parameters, return_type, attributes, body )

    method init( t, module_context, name, parameters=null, return_type=null,
        attributes=null, body=null )
      if (not parameters) parameters = CmdList(t)
      if (not attributes) attributes = Attributes( t )
      if (not body)       body = LocalScope(t)
      statements = body->(as LocalScope).statements

    method init( existing:Procedure )
      prior.init( existing )
      name = existing.name
      if (existing.parameters)  parameters  = existing.parameters.cloned
      if (existing.return_type) return_type = existing.return_type.cloned
      if (existing.attributes)  attributes  = existing.attributes.cloned
      if (existing.body)        body  = existing.body.cloned
      signature = existing.signature
      if (body) statements = body->(as LocalScope).statements

    method description->String
      return "Procedure($,$,$,$,$)" (%name,%parameters,%return_type,%attributes,%body)

    method organize
      if (not signature)
        use builder = StringBuilder.pool
          builder.print name
          builder.print '('
          builder.print ')'
          signature = builder->String
        endUse
      endIf

    method resolve
      if (is_resolved) return
      is_resolved = true
      Resolver( module_context ).visit( null, this )

endClass

