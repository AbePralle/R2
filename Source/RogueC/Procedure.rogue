module Rogue

class Procedure : Cmd
  PROPERTIES
    module_context : Module
    name           : String
    signature      : String
    parameters     : Cmd
    return_type    : Type
    attributes     : Attributes
    body           : LocalScope   # LocalScope(statements)
    statements     : Cmd   # the Statements contained in the body's LocalScope
    is_resolved    = false

    min_args       : Int32

    locals         = Local[]

  METHODS
    method init( t:Token, name:String, parameters=null:Cmd, return_type=null:Type,
        attributes=null:Attributes, body=null:LocalScope )
      init( t, null, name, parameters, return_type, attributes, body )

    method init( t, module_context, name, parameters=null, return_type=null,
        attributes=null, body=null )
      if (not parameters) parameters = CmdList(t)
      if (not attributes) attributes = Attributes( t )
      if (not body)       body = LocalScope(t)
      statements = body.statements

    method init( existing:Procedure )
      prior.init( existing )
      name = existing.name
      if (existing.parameters)  parameters  = existing.parameters.cloned
      if (existing.return_type) return_type = existing.return_type.cloned
      if (existing.attributes)  attributes  = existing.attributes.cloned
      if (existing.body)        body  = existing.body.cloned
      signature = existing.signature
      if (body) statements = body.statements

    method accepts_arg_count( n:Int32 )->Logical
      return n >= min_args and n <= parameters.count

    method context_signature->String
      return "$::$"(module_context,signature)

    method description->String
      return "Procedure($,$,$,$,$)" (%name,%parameters,%return_type,%attributes,%body)

    method organize
      if (not signature)
        if (not module_context) module_context = Program.current_module

        use builder = StringBuilder.pool
          builder.print name
          builder.print '('
          forEach (p at i in parameters)
            block p = p->(as Local)
              assert p
              if (i) builder.print ','
              if (not p.type) throw p.t.error( "Missing parameter type (e.g. $:SomeType)."(p.name) )
              builder.print( p.type.name )
            endBlock
          endForEach
          builder.print ')'
          signature = builder->String
        endUse

        min_args = 0
        forEach (p in parameters)
          block p = p->(as Local)
            if (p.initial_value) escapeForEach
            ++min_args
          endBlock
        endForEach
        forEach (p in parameters from min_args)
          block p = p->(as Local)
            if (not p.initial_value)
              throw p.t.error( "Missing default value. Once one parameter has a default value, all successive parameters must supply default values as well." )
            endIf
          endBlock
        endForEach
      endIf

    method resolve
      if (is_resolved) return
      is_resolved = true
      Resolver( module_context ).visit( null, this )

endClass

