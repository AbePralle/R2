module Rogue

$include "Macros.rogue"

$include "AspectMethodPatcher.rogue"
$include "Attributes.rogue"
$include "Candidates.rogue"
$include "Collector.rogue"
$include "ContextBlockPatcher.rogue"
$include "ControlType.rogue"
$include "CmdType.rogue"
$include "CGenerator.rogue"
$include "CSpecializer.rogue"
$include "CWriter.rogue"
$include "DecompositionAnalyzer.rogue"
$include "EscapeAnalyzer.rogue"
$include "Finalizer.rogue"
$include "InspectionVisitor.rogue"
$include "Module.rogue"
$include "Organizer.rogue"
$include "Parser.rogue"
$include "Procedure.rogue"
$include "Program.rogue"
$include "Resolver.rogue"
$include "Scanner.rogue"
$include "Template.rogue"
$include "Type.rogue"
uses Rogue
uses Utility/CommandLineParser

try
  RogueC( System.command_line_arguments )

catch (error:CompileError)
  Console.error.println error
  System.exit 1
catch (error:Error)
  Console.error.println error
  Console.error.println error.stack_trace
  System.exit 1
endTry

class RogueC
  PROPERTIES
    command : Value

  METHODS
    method init( args:String[] )
      command = parse_args( args )

      if (command//options//help or command//args.count == 0)
        print_usage
        System.exit 0
      endIf

      if (command//options//libraries)
        local paths = String[]
        forEach (lib in command//options//libraries)
          local path = lib->String.replacing(';',',')
          if (not System.is_windows) path .= replacing(':',',')
          paths.add( forEach in path->String.split(',') )
        endForEach
        command//options//libraries = paths
        Program.add_include_search_path( forEach in paths )
      endIf

      if (command//options//real)
        local real_bits = command//options//real->Int32
        if (real_bits != 32 and real_bits != 64)
          throw Error( "--real can only be set to 32 or 64." )
        endIf
        Program.real_bits = real_bits
      endIf

      Program.using_arc = true
      Program.include( forEach in command//args )
      Program.include( "Standard" )
      Program.resolve
      Program.generate_c

    method parse_args( args:String[] )->Value
      local command = CommandLineParser().
      [
        #option( "--flag",      &alias="-f" )
        option( "--help",       &aliases=["-h","-?"] )
        option( "--libraries=", &alias="-L", &multi )
        option( "--real=", &alias="-r" )
        #option( "--setting=", &alias="-s" )
      ].parse( args )
      return command

    method print_usage
      println @|USAGE
               |  rogue [OPTIONS] <filepath>
               |
               |OPTIONS
               |  --help, -h, -?
               |    Show this help text.
               |
               |  --libraries="path1;path2", -L path1:path2
               |    Specify paths RogueC should look through for '$include' and 'uses' directives.
               |    This option can be listed multiple times. Both ';' and ':' can be used as a
               |    path separator on quoted paths.
               |
               |  --real=32, -r 32
               |    Specifies that the 'Real' datatype should be Real32 instead of Real64.
               |    This affects the type of floating point literals (2.54, pi) as well.

endClass

